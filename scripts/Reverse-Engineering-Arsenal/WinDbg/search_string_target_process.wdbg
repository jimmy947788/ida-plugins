$$
$$ Author: Javier Vicente Vallejo
$$ Twitter: @vallejocc
$$ Web: http://www.vallejo.cc
$$
$$     $$>a<search_string_target_process.wdbg <proc> <string> 
$$
$$ This script is useful for search a given string in a given process
$$

aS baseSearch @$t17
aS stop @$t16

.block
{
    .sympath "SRV*c:\symcache*http://msdl.microsoft.com/download/symbols";
    .reload
}

.block
{
    .process /i ${$arg1}
    g
    
    .block
    {
        .reload
    }
    
    .block
    {
        .reload /user
    }
    
    r ${stop} = 0
    r ${baseSearch} = 0
    
    .while(${stop} == 0)
    {
        .printf "searching base %x\n", ${baseSearch}
        
        r @$t0 = ${baseSearch}
        
        s -a @$t0 L?0x10000000 "${$arg2}"
        
        .if(${baseSearch} >= 0x70000000)
        {
            r ${stop} = 1
        }
        
        r ${baseSearch} = ${baseSearch} + 0x10000000            
    }

    ad baseSearch
    ad stop
}